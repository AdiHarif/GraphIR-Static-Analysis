
#include "./graph_definitions.dl"
#include "./type_inference.dl"
#include "./points_to.dl"

.decl array_element_type(array: number, type: symbol)

array_element_type(array, type) :-
    vertex(array, "Allocation", _, _),
    inferred_type(array, "Array"),
    vertex(store, "Store", _, _),
    edge(store, array_ref, _, "object"),
    may_refer_to(array_ref, array),
    edge(store, value, _, "value"),
    inferred_type(value, type).

array_element_type(array, type) :-
    vertex(array, "Allocation", _, _),
    inferred_type(array, "Array"),
    edge(array, _, _, "1"), // c'tor has more than one parameter
    edge(array, element, _, index),
    IS_NUMERIC(index),
    inferred_type(element, type).

inferred_type(load, type) :-
    vertex(load, "Load", _, _),
    edge(load, array_ref, _, "object"),
    may_refer_to(array_ref, array),
    array_element_type(array, type).

.output array_element_type()