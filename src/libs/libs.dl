
#pragma once

.decl lib_method(lib: symbol, method: symbol)

inferred_type(v, $Symbol()) :-
    vertex(v, "Load", _, _),
    lib_method(lib, method),
    edge(v, lib_id, _, "object"),
    vertex(lib_id, "Symbol", _, lib),
    edge(v, method_id, _, "property"),
    vertex(method_id, "Symbol", _, method).

inferred_type(id, type) :-
    vertex(id, "Call", _, _),
    edge(id, symbol_id, _, "callee"),
    vertex(symbol_id, "Load", _, _),
    edge(symbol_id, method_id, _, "property"),
    vertex(method_id, _, _, method),
    lib_method(lib, method),
    full_name = cat(lib, cat("_", method)),
    function_return_type(full_name, type).


.decl refers_to_function(v: number, f: symbol)

refers_to_function(v, full_name) :-
    vertex(v, "Load", _, _),
    edge(v, function_id, _, "property"),
    vertex(function_id, "Symbol", _, method_name),
    edge(v, lib_id, _, "object"),
    vertex(lib_id, "Symbol", _, lib_name),
    lib_method(lib_name, method_name),
    full_name = cat(lib_name, cat("_", method_name)).


.decl lib_variable(lib: symbol, name: symbol, type: irType<>)

inferred_type(load, type) :-
    lib_variable(lib, name, type),
    vertex(load, "Load", _, _),
    edge(load, property, _, "property"),
    vertex(property, "Symbol", _, name),
    edge(load, object, _, "object"),
    vertex(object, "Symbol", _, lib).
