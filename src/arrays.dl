
#pragma once

#include "graph_definitions.dl"
#include "type_inference.dl"
#include "points_to.dl"
#include "ir_type.dl"

.decl array_element_type(array: number, type: irType<>)

array_element_type(array, type) :-
    vertex(array, "Allocation", _, _),
    inferred_type(array, $Array($Bottom())),
    vertex(store, "Store", _, _),
    edge(store, array_ref, _, "object"),
    may_refer_to(array_ref, array),
    edge(store, value, _, "value"),
    inferred_type(value, type).

array_element_type(array, type) :-
    vertex(array, "Allocation", _, _),
    inferred_type(array, $Array($Bottom())),
    edge(array, _, _, "1"), // c'tor has more than one parameter
    edge(array, element, _, index),
    IS_NUMERIC(index),
    inferred_type(element, type).

array_element_type(array, $Undefined()) :-
    vertex(array, "Allocation", _, _),
    inferred_type(array, $Array($Bottom())),
    !edge(array, _, _, "1"), // c'tor has only one parameter
    edge(array, element, _, "0"),
    vertex(element, "Literal", _, value),
    IS_NUMERIC(value).

inferred_type(load, type) :-
    vertex(load, "Load", _, _),
    edge(load, array_ref, _, "object"),
    may_refer_to(array_ref, array),
    array_element_type(array, type).
