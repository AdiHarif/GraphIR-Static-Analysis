
#pragma once

#include "ir_type.dl"

.decl edge(src: number, dst: number, category: symbol, label: symbol)
.input edge(filename="edges.csv", delimiter=",")

.decl vertex(id: number, kind: symbol, category: symbol, label: symbol)
.input vertex(filename="vertices.csv", delimiter=",")

.decl data_vertex(v: number)
data_vertex(v) :- vertex(v, _, "Data", _).
data_vertex(v) :- vertex(v, _, "Compound", _).

// ! phi_args asumes there are exactly two arguments to a phi node
.decl phi_args(phi: number, arg1: number, arg2: number) choice-domain phi
phi_args(phi, arg1, arg2) :-
    vertex(phi, "Phi", _, _),
    edge(phi, arg1, "Data", _),
    edge(phi, arg2, "Data", _),
    arg1 != arg2.


.decl control_vertex(v: number)
control_vertex(v) :- vertex(v, _, "Control", _).
control_vertex(v) :- vertex(v, _, "Compound", _).

.decl reaching(src: number, dest: number)
reaching(a, a) :- control_vertex(a).
reaching(a, b) :- reaching(a, mid), edge(mid, b, "Control", _).

.decl function_return(function: symbol, vertex: number)
// .input function_return(filename="function_return.csv", delimiter=",")
function_return(function, return) :-
    vertex(return, "Return", _, _),
    vertex(start, "Start", _, _),
    reaching(start, return),
    vertex(symbol, "Symbol", _, function),
    edge(symbol, start, _, _).

.decl function_parameter(function: symbol, vertex: number, index: symbol)
function_parameter(function, parameter, index) :-
    vertex(symbol, "Symbol", _, function),
    vertex(parameter, "Parameter", _, _),
    edge(symbol, parameter, "Association", index).
