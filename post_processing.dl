
#include "type_inference.dl"
#include "arrays.dl"
#include "points_to.dl"
#include "src/symbol_list.dl"

.decl full_type(vertex: number, type: symbol)

.decl primitive_type_conversion(source_type: symbol, target_type: symbol)
primitive_type_conversion("number", "Float64").
primitive_type_conversion("boolean", "Integer1").
primitive_type_conversion("undefined", "Void").

full_type(v, target) :-
    inferred_type(v, source),
    primitive_type_conversion(source, target).

full_type(v, cat("DynamicArray<", cat(element_type,">"))) :-
    inferred_type(v, "Array"),
    may_refer_to(v, alloc),
    array_element_type(alloc, source_type),
    primitive_type_conversion(source_type, element_type).

full_type(v, cat("DynamicArray<", cat(element_type,">"))) :-
    inferred_type(v, "Array"),
    may_refer_to(v, alloc),
    points_to(alloc, element, _),
    full_type(element, element_type).


.decl params_list(function_vertex: number, params: SymbolList, amount: number)

params_list(function_vertex, nil, 0) :-
    vertex(function_vertex, "Symbol", _, _),
    edge(function_vertex, _, _, "start").

params_list(function_vertex, @list_append(params, type), n + 1) :-
    params_list(function_vertex, params, n),
    edge(function_vertex, param_vertex, _, to_string(n)),
    full_type(param_vertex, type).

params_list(function_vertex, nil, 0) :-
    refers_to_function(function_vertex, _).

params_list(function_vertex, @list_append(params, full_type), n + 1) :-
    params_list(function_vertex, params, n),
    refers_to_function(function_vertex, function_name),
    function_parameters_type(function_name, to_string(n), type),
    primitive_type_conversion(type, full_type).

params_list(f, _, n1) <= params_list(f, _, n2) :- n1 < n2.

full_type(function_vertex, type) :-
    (
        vertex(function_vertex, _, _, function_name) ;
        refers_to_function(function_vertex, function_name)
    ),
    params_list(function_vertex, p_list, _),
    params = cat("[", cat(@list_join_by_string(p_list, ","), "]")),
    function_return_type(function_name, return_type),
    primitive_type_conversion(return_type, full_return_type),
    type = cat(params, cat("->", full_return_type)).

// TODO: replace this with something more robust
full_type(f, t1) <= full_type(f, t2) :- strlen(t1) < strlen(t2).

.output full_type
